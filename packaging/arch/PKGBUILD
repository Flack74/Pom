# Maintainer: Flack <puspendrachawlax@gmail.com>
pkgname=pom
pkgver=1.0.2
pkgrel=4
pkgdesc="A beautiful and feature-rich CLI Pomodoro timer with notifications and sound alerts"
url="https://github.com/Flack74/pom"
arch=("x86_64" "aarch64")
license=("MIT")
depends=("libnotify" "pulseaudio" "zenity")
makedepends=("go" "git")
source=("git+https://github.com/Flack74/pom.git#tag=v${pkgver}")
sha256sums=("SKIP")

build() {
    # Verify we're on the correct tag
    git -C "$pkgname" checkout "v${pkgver}" -f

    cd "$pkgname"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
    
    _builddate=$(date -u '+%Y-%m-%d %H:%M:%S UTC')
    _commit=$(git rev-parse --short HEAD)
    
    go build \
        -ldflags="-linkmode=external \
                  -X 'cmd.version=${pkgver}' \
                  -X 'cmd.buildDate=${_builddate}' \
                  -X 'cmd.gitCommit=${_commit}'" \
        -o pom
}

package() {
    cd "$pkgname"
    install -Dm755 pom "$pkgdir/usr/bin/pom"
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}